model{

#--------#
#-PRIORS-#
#--------#

#Biological process priors
lambda0 ~ dnorm(0, 0.1) #Intercept
beta1 ~ dnorm(0, 0.1) #Effect of environment

#Presence only observation process priors
p0 ~ dnorm(0, 0.368) #Intercept
alpha1 ~ dnorm(0, 0.368) #Effect of sampling intensity

#Distance sampling observation process priors
sigma ~ dunif(0, 10) #Scale parameter

#------------#
#-LIKELIHOOD-#
#------------#

for(g in 1:G){

#Observation process of opportunistic presence-only data (thinned point process)
y[g] ~ dpois(p[g] * lambda[g])

#Linear predictor for observation process of opportunistic presence-only data
logit(p[g]) <- p0 + alpha1 * z[g]

#Linear predictor for biological process
lambda[g] <- exp(lambda0 + beta1 * w[g])

}#end g

for(b in 1:B){

#Half-normal distance function
pi[b] <- exp(-d[b]*d[b]/(2*sigma*sigma)) #Detection probability of DS

#Observation process of distance sampling data (thinned point process)
x[b] ~ dpois(pi[b] * lambda[coverage[b]])

}#end b

#----------------#
#-DERIVED VALUES-#
#----------------#    

#Abundance
Ntot <- sum(lambda[])

}